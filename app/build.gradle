apply plugin: 'com.android.application'
apply plugin: 'com.google.android.gms.oss-licenses-plugin'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-parcelize'

// Load local properties with API keys
def localProperties = new Properties()
localProperties.load(new FileInputStream(rootProject.file("local.properties")))

android {
    compileSdkVersion 30
    buildToolsVersion '29.0.2'
    defaultConfig {
        applicationId 'com.b_lam.resplash'
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 47
        versionName '3.2.4'
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        buildConfigField("String", "RELEASE_APP_ID", localProperties['releaseAppId'])
        buildConfigField("String", "RELEASE_SECRET", localProperties['releaseSecret'])
        buildConfigField("String", "DEV_APP_ID", localProperties['devAppId'])
        buildConfigField("String", "DEV_SECRET", localProperties['devSecret'])
        buildConfigField("String", "GOOGLE_PLAY_LICENSE_KEY", localProperties['googlePlayLicenseKey'])
        renderscriptTargetApi 30 as int
        renderscriptSupportModeEnabled true
        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            applicationIdSuffix '.debug'
        }
    }
    buildFeatures {
        viewBinding true
    }
    bundle {
        language {
            enableSplit = false
        }
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    lintOptions {
        disable 'MissingTranslation'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Support libraries
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.browser:browser:1.3.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.fragment:fragment-ktx:1.3.3'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.paging:paging-runtime-ktx:2.1.2'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'androidx.recyclerview:recyclerview:1.2.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.work:work-runtime-ktx:2.6.0-alpha02'

    // Lifecycle
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.3.1'
    implementation 'androidx.lifecycle:lifecycle-service:2.3.1'

    // Room
    implementation 'androidx.room:room-runtime:2.3.0'
    implementation 'androidx.room:room-ktx:2.3.0'
    kapt 'androidx.room:room-compiler:2.3.0'

    // Material components
    implementation 'com.google.android.material:material:1.3.0'

    // Billing
    implementation 'com.android.billingclient:billing:3.0.3'
    implementation 'com.android.billingclient:billing-ktx:3.0.3'

    // Kotlin coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.3'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.3'

    // OkHttp
    implementation 'com.squareup.okhttp3:okhttp:4.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.0'

    // Okio
    implementation 'com.squareup.okio:okio:2.9.0'

    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-moshi:2.9.0'

    // Moshi
    implementation 'com.squareup.moshi:moshi:1.11.0'
    kapt 'com.squareup.moshi:moshi-kotlin-codegen:1.11.0'

    // Glide
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    implementation 'jp.wasabeef:glide-transformations:4.3.0'
    kapt 'com.github.bumptech.glide:compiler:4.12.0'

    // Koin
    implementation 'org.koin:koin-android:2.2.2'
    implementation 'org.koin:koin-androidx-viewmodel:2.2.2'
    implementation 'org.koin:koin-androidx-workmanager:2.2.2'

    // Firebase
    implementation 'com.google.firebase:firebase-analytics-ktx:18.0.3'
    implementation 'com.google.firebase:firebase-crashlytics:17.4.1'
    implementation 'com.google.firebase:firebase-inappmessaging-ktx:19.1.5'
    implementation 'com.google.firebase:firebase-firestore-ktx:22.1.2'
    implementation 'com.google.guava:listenablefuture:9999.0-empty-to-avoid-conflict-with-guava'

    // Play Core
    implementation 'com.google.android.play:core:1.10.0'
    implementation 'com.google.android.play:core-ktx:1.8.1'

    // View binding
    implementation 'com.github.kirich1409:viewbindingpropertydelegate:1.4.4'
    
    // Lottie
    implementation 'com.airbnb.android:lottie:3.5.0'

    // PhotoView
    implementation 'com.github.chrisbanes:PhotoView:2.3.0'

    // Pager indicator
    implementation 'com.github.intik:overflow-pager-indicator:3.1.0'

    // PrettyTime
    implementation 'org.ocpsoft.prettytime:prettytime:4.0.4.Final'

    // Open source notices
    implementation 'com.google.android.gms:play-services-oss-licenses:17.0.0'

    // Muzei
    implementation 'com.google.android.apps.muzei:muzei-api:3.4.0'

    // LeakCanary
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.6'

    // Unit tests
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'

    // Room testing
    androidTestImplementation "androidx.room:room-testing:2.3.0"
}
